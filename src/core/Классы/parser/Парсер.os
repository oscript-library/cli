
//#Использовать "../"

Перем Спек;

Перем Опции; // Структура Ключ - Значение (Структура описание опции)
Перем Аргументы; // Структура  

Перем ОпцииИндекс; // Соответствие
Перем АргументыИндекс; // Соответствие

Перем ТокеныПарсера;

Перем ТекущаяПозиция;
Перем НайденныйТокен;

Перем СкинутьОпции;
Перем ТипыТокенов;

Перем fsm;

перем Лог;

Процедура ПриСозданииОбъекта(Знач ТокеныСпек, Знач ПараметрыПарсера)

	ТокеныПарсера = ТокеныСпек;
	Опции = ПараметрыПарсера.Опции;
	Аргументы = ПараметрыПарсера.Аргументы;
	ОпцииИндекс = ПараметрыПарсера.ОпцииИндекс;
	АргументыИндекс = ПараметрыПарсера.АргументыИндекс;
	Спек = ПараметрыПарсера.Спек;
	
	Лог.Отладка("Спек %1", Спек);
	Лог.Отладка("Количество опций: %1", Опции.Количество());
	
	ТекущаяПозиция = 0;

	СкинутьОпции = Ложь;

	ТипыТокенов = Токены.ТипыТокенов();
	
	fsm = Новый ВыборСовпадений();
	
КонецПроцедуры

Функция Прочитать() Экспорт


	Лог.Отладка("Начинаю чтение спека %1", Спек);
	Лог.Отладка("Количество токенов %1", ТокеныПарсера.Количество());
	

	Результат = ПрочитатьРекурсивно(Ложь);
		
	Если Не КонецТокенов() Тогда
		ВызватьИсключение "Косяк ппц что делать";
	КонецЕсли;

	НачальноеСостояние = Результат.НачальноеСостояние;
	КонечноеСостояние = Результат.КонечноеСостояние;
	
	Лог.Отладка("Прочитать() Пред .Подготовить() >> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
	Лог.Отладка("Прочитать() Пред .Подготовить() >> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());

	НачальноеСостояние.Подготовить();
	КонечноеСостояние.Завершено = Истина;
	Лог.Отладка("Выход Прочитать() >> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
	Лог.Отладка("Выход Прочитать() >> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());

	Возврат НачальноеСостояние;

КонецФункции

Функция ПрочитатьРекурсивно(Требуется)

	Лог.Отладка("Рекурсивное чтение %1", Требуется);
	

	НачальноеСостояние = fsm.НовоеСостояние();
	КонечноеСостояние = НачальноеСостояние;


	Если Требуется Тогда

		Результат = ТокенВыбора();
		
		appendComp(КонечноеСостояние, Результат.НачальноеСостояние, Результат.КонечноеСостояние);
		Лог.Отладка("ТокенВыбора Выход Требуется >> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
		Лог.Отладка("ТокенВыбора Выход Требуется >> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());
	
	КонецЕсли;

	Пока МогуПрочитать() Цикл

		РезультатВЦикле = ТокенВыбора();
		
		appendComp(КонечноеСостояние, РезультатВЦикле.НачальноеСостояние, РезультатВЦикле.КонечноеСостояние);
		Лог.Отладка("ТокенВыбора Выход цикл >> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
		Лог.Отладка("ТокенВыбора Выход цикл >> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());

	КонецЦикла;
	Лог.Отладка("Выход из рекурсии >> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
	Лог.Отладка("Выход из рекурсии >> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());

	возврат Новый Структура("НачальноеСостояние, КонечноеСостояние", НачальноеСостояние, КонечноеСостояние);
		
КонецФункции

Процедура appendComp(КонечноеСостояние, s, e)
	
	Лог.Отладка("appendComp цикл >> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());
	Лог.Отладка("appendComp цикл >> s.МассивСоединений: %1", s.МассивСоединений.Количество());

	Для каждого Соединение Из s.МассивСоединений Цикл
		КонечноеСостояние.Т(Соединение.Парсер, Соединение.СледующееСостояние);
	КонецЦикла;

	КонечноеСостояние = e;

КонецПроцедуры

Функция ЧтениеДалее()
	
	Лог.Отладка("Вызов <ЧтениеДалее>");

	НачальноеСостояние = fsm.НовоеСостояние();
	КонечноеСостояние = Неопределено;

	Если КонецТокенов() Тогда
		ВызватьИсключение "Не правильная строка использования";

	ИначеЕсли НашлиТокен(ТипыТокенов.TTArg) Тогда

		Имя = НайденныйТокен.Значение;

		КлассОпции = АргументыИндекс[Имя];
		Если КлассОпции = Неопределено Тогда
			Назад();
			//Сообщить("Нашли не объявленный аргумент");
			ВызватьИсключение "Нашли не объявленный аргумент";
		КонецЕсли;

		КонечноеСостояние = НачальноеСостояние.Т(Новый АргументыПарсера(КлассОпции), fsm.НовоеСостояние());
		
	ИначеЕсли НашлиТокен(ТипыТокенов.TTOptions) Тогда

		Если СкинутьОпции Тогда
			Назад();
			Сообщить("нет опций после ---");
			ВызватьИсключение "нет опций после ---";
		КонецЕсли;


		КонечноеСостояние = НачальноеСостояние.Т(Новый ВсеОпцииПарсера(Опции, ОпцииИндекс), fsm.НовоеСостояние());
	ИначеЕсли НашлиТокен(ТипыТокенов.TTShortOpt)
		ИЛИ НашлиТокен(ТипыТокенов.TTLongOpt) Тогда

		Лог.Отладка("Обрабатываю токен: %1", НайденныйТокен.Тип);
		Если СкинутьОпции Тогда
			Назад();
			Сообщить("нет опций после ---");
			ВызватьИсключение "нет опций после ---";
		КонецЕсли;

		Имя = НайденныйТокен.Значение;
		КлассОпции = ОпцииИндекс[Имя];
		Лог.Отладка("		>> Имя токен: %1", НайденныйТокен.Значение);
		Лог.Отладка("		>> класс опции: %1", КлассОпции.Имя);
	
		Если КлассОпции = Неопределено Тогда
			Назад();
			Сообщить("Нашли не объявленную опцию");
			ВызватьИсключение "Ошибка";
		КонецЕсли;


		КонечноеСостояние = НачальноеСостояние.Т(Новый ОпцияПарсера(КлассОпции, ОпцииИндекс), fsm.НовоеСостояние());
		Лог.Отладка("		>> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
		
		НашлиТокен(ТипыТокенов.TTOptValue); // Пропуск значение после "=" 

	ИначеЕсли НашлиТокен(ТипыТокенов.TTOptSeq) Тогда

		Если СкинутьОпции Тогда
			Назад();
			Сообщить("нет опций после ---");
			ВызватьИсключение "нет опций после ---";
		КонецЕсли;

		КонечноеСостояние = fsm.НовоеСостояние();

		ДоступныеОпции = НайденныйТокен.Значение;
		Если СтрНачинаетсяС(ДоступныеОпции, "-") Тогда
			ДоступныеОпции = Прав(ДоступныеОпции, СтрДлина(ДоступныеОпции)-1);
		КонецЕсли;
		МассивДоступныхОпций  = Новый Соответствие;
		ДлинаОпций = СтрДлина(ДоступныеОпции);
		Для ИИ = 1 По ДлинаОпций Цикл
			
			ИмяОпции = Сред(ДоступныеОпции, ИИ, 1);
			
			КлассОпции = ОпцииИндекс["-"+ИмяОпции];
			
			Если КлассОпции = Неопределено Тогда
				Назад();
				Сообщить("Нашли не объявленную опцию");
				ВызватьИсключение "Ошибка Нашли не объявленную опцию";
			КонецЕсли;
	
			МассивДоступныхОпций.Вставить(КлассОпции, КлассОпции);

		КонецЦикла;		

		НачальноеСостояние.Т(Новый ВсеОпцииПарсера(МассивДоступныхОпций, ОпцииИндекс), КонечноеСостояние);

	ИначеЕсли НашлиТокен(ТипыТокенов.TTOpenPar) Тогда

		РезультатЧтения = ПрочитатьРекурсивно(Истина);

		НачальноеСостояние = РезультатЧтения.НачальноеСостояние;
		КонечноеСостояние = РезультатЧтения.КонечноеСостояние;
		
		ОжидаюТокен(ТипыТокенов.TTClosePar);
		
	ИначеЕсли НашлиТокен(ТипыТокенов.TTOpenSq) Тогда
		РезультатЧтения = ПрочитатьРекурсивно(Истина);
		
		НачальноеСостояние = РезультатЧтения.НачальноеСостояние;
		КонечноеСостояние = РезультатЧтения.КонечноеСостояние;
		НачальноеСостояние.Т(Новый ЛюбойСимвол(), КонечноеСостояние);
		
		ОжидаюТокен(ТипыТокенов.TTCloseSq);

	ИначеЕсли НашлиТокен(ТипыТокенов.TTDoubleDash) Тогда
		
		СкинутьОпции = Истина;
		КонечноеСостояние = НачальноеСостояние.Т(Новый ОпцииЗавершениеПарсера(), fsm.НовоеСостояние());
		
		возврат Новый Структура("НачальноеСостояние, КонечноеСостояние", НачальноеСостояние, КонечноеСостояние);

	КонецЕсли;

	Если НашлиТокен(ТипыТокенов.TTRep) Тогда
		КонечноеСостояние.Т(Новый ЛюбойСимвол(), НачальноеСостояние);
	
	КонецЕсли;

	Если КонечноеСостояние = Неопределено Тогда
		ВызватьИсключение "Не могу правильно построить маршрут";
	КонецЕсли;
	возврат Новый Структура("НачальноеСостояние, КонечноеСостояние", НачальноеСостояние, КонечноеСостояние);
	
КонецФункции

Функция ТокенВыбора()
	
	Лог.Отладка("Вызов <ТокенВыбора>");
		
	НачальноеСостояние = fsm.НовоеСостояние();
	КонечноеСостояние = fsm.НовоеСостояние();

	Результат = ЧтениеДалее();
	
	Лог.Отладка("ТокенВыбора >> НачальноеСостояние.МассивСоединений: %1", Результат.НачальноеСостояние.МассивСоединений.Количество());
	Лог.Отладка("ТокенВыбора >> КонечноеСостояние.МассивСоединений: %1", Результат.КонечноеСостояние.МассивСоединений.Количество());
	
	//ВыборТокена_Адд(НачальноеСостояние, КонечноеСостояние, Результат.НачальноеСостояние, Результат.КонечноеСостояние);

	НачальноеСостояние.Т(Новый ЛюбойСимвол(), Результат.НачальноеСостояние);
	Результат.КонечноеСостояние.Т(Новый ЛюбойСимвол(), КонечноеСостояние);

	Лог.Отладка("ТокенВыбора АДД >> НачальноеСостояние.МассивСоединений: %1", НачальноеСостояние.МассивСоединений.Количество());
	Лог.Отладка("ТокенВыбора АДД>> КонечноеСостояние.МассивСоединений: %1", КонечноеСостояние.МассивСоединений.Количество());

	Пока НашлиТокен(ТипыТокенов.TTChoice) Цикл
		РезультатВЦикле = ЧтениеДалее();
		
		НачальноеСостояние.Т(Новый ЛюбойСимвол(), РезультатВЦикле.НачальноеСостояние);
		РезультатВЦикле.КонечноеСостояние.Т(Новый ЛюбойСимвол(), КонечноеСостояние);
	
		//ВыборТокена_Адд(НачальноеСостояние, КонечноеСостояние, РезультатВЦикле.НачальноеСостояние, РезультатВЦикле.КонечноеСостояние);
	КонецЦикла;
	
	возврат Новый Структура("НачальноеСостояние, КонечноеСостояние", НачальноеСостояние, КонечноеСостояние);
	
КонецФункции

Функция ВыборТокена_Адд(НачальноеСостояние, КонечноеСостояние, s, e)
	
	НачальноеСостояние.Т(Новый ЛюбойСимвол(), s);
	e.Т(Новый ЛюбойСимвол(), НачальноеСостояние);

КонецФункции

Функция ОжидаюТокен(ТипТокена)

	Если НЕ НашлиТокен(ТипТокена) Тогда
		ВызватьИсключение "Не найден ожидаемый токен " + ТипТокена;
	КонецЕсли;
	
КонецФункции

Функция КонецТокенов()

	//Лог.Отладка("Вызов <КонецТокенов> %1 >= %2", ТекущаяПозиция, Токены.Количество());
	
	Возврат ТекущаяПозиция >= ТокеныПарсера.Количество();
			
КонецФункции

Процедура Назад()
	
	ТекущаяПозиция = ТекущаяПозиция-1;
			
КонецПроцедуры

Функция Токен()

	Если КонецТокенов() Тогда
		возврат Неопределено;
	КонецЕсли;

	Возврат ТокеныПарсера[ТекущаяПозиция];
	
КонецФункции

Функция ЭтоТокен(Знач ТипТокена)

	Если КонецТокенов() Тогда
		возврат Ложь;
	КонецЕсли;

	
	//Лог.Отладка("Проверяю текущий токен:
	//| >> Сверка типов: %1 = %2", Токен().Тип, ТипТокена);
	Возврат Токен().Тип = ТипТокена;
	
КонецФункции

Функция МогуПрочитать()

	//Лог.Отладка("Вызов <МогуПрочитать>");

	Если ЭтоТокен(ТипыТокенов.TTArg)
		ИЛИ ЭтоТокен(ТипыТокенов.TTOptions)
		ИЛИ ЭтоТокен(ТипыТокенов.TTShortOpt)
		ИЛИ ЭтоТокен(ТипыТокенов.TTLongOpt)
		ИЛИ ЭтоТокен(ТипыТокенов.TTOptSeq)
		ИЛИ ЭтоТокен(ТипыТокенов.TTOpenPar)
		ИЛИ ЭтоТокен(ТипыТокенов.TTOpenSq)
		ИЛИ ЭтоТокен(ТипыТокенов.TTDoubleDash)
		Тогда

		Возврат Истина;

	Иначе

		Возврат  Ложь
	КонецЕсли;	

	
КонецФункции

Функция НашлиТокен(Знач ТипТокена)

	Если ЭтоТокен(ТипТокена) Тогда

		Лог.Отладка("Нашли токен: %1", ТипТокена);
		НайденныйТокен = Токен();
		
		ТекущаяПозиция = ТекущаяПозиция +1;

		ЛОг.Отладка("Текущая позиция: %1", ТекущаяПозиция);

		Возврат Истина;

	КонецЕсли;

	Возврат Ложь;

КонецФункции


Лог = Логирование.ПолучитьЛог("oscript.lib.spec_parse");
//Лог.УстановитьУровень(УровниЛога.Отладка);