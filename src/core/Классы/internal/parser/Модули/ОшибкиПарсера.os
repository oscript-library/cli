Функция НоваяОшибка(Текст, Приоритет = 0) Экспорт
	Возврат Новый Структура("Текст, Приоритет", Текст, Приоритет);
КонецФункции

Функция ОшибкиВСтроку(Ошибки, ТолькоСМаксимальнымПриоритетом = Ложь) Экспорт

	Разделитель = "; ";

	МаксПриоритет = Неопределено;
	Если ТолькоСМаксимальнымПриоритетом Тогда
		МаксПриоритет = 0;
		Для Каждого Ошибка Из Ошибки Цикл
			МаксПриоритет = Макс(МаксПриоритет, Ошибка.Приоритет);
		КонецЦикла;
	КонецЕсли;

	МассивОшибок = Новый Массив();
	Для Каждого Ошибка Из Ошибки Цикл
		Если (Ошибка.Приоритет = МаксПриоритет ИЛИ МаксПриоритет = Неопределено)
			И МассивОшибок.Найти(Ошибка.Текст) = Неопределено Тогда
			МассивОшибок.Добавить(Ошибка.Текст);
		КонецЕсли;
	КонецЦикла;

	Возврат СтрСоединить(МассивОшибок, Разделитель);

КонецФункции

Функция ОшибкаТребуетВнимания(Ошибка) Экспорт
	Возврат Ошибка.Приоритет > 1;
КонецФункции

#Область ОшибкиПрочие

Функция ОшибкаНеожидаемыйПараметр(Имя) Экспорт
	Текст = СтрШаблон("Неожидаемый параметр %1", Имя);
	Возврат НоваяОшибка(Текст, 1);
КонецФункции

#КонецОбласти

#Область ОшибкиОпции

Функция ОшибкаТребуетсяЗначениеДляОпции(Имя) Экспорт
	Текст = СтрШаблон("Опция %1 должна содержать значение", ВосстановитьТиреПередОпцией(Имя));
	Возврат НоваяОшибка(Текст, 2);
КонецФункции

Функция ОшибкаТребуетсяЗначениеПослеЗнакаРавноДляОпции(Имя) Экспорт
	Текст = СтрШаблон("Опция %1 должна содержать значение после знака =", ВосстановитьТиреПередОпцией(Имя));
	Возврат НоваяОшибка(Текст, 2);
КонецФункции

Функция ОшибкаНеожидаемаяОпция(Имя) Экспорт
	Текст = СтрШаблон("Неожидаемая опция %1", ВосстановитьТиреПередОпцией(Имя));	
	Возврат НоваяОшибка(Текст, 1);
КонецФункции

Функция ОшибкаОжидаетсяОпция(Имя) Экспорт
	Текст = СтрШаблон("Ожидается опция %1", ВосстановитьТиреПередОпцией(Имя));
	Возврат НоваяОшибка(Текст, 1);
КонецФункции

Функция ОшибкаОжидаетсяОпцияВместо(Имя, ИмяВместо) Экспорт
	Текст = СтрШаблон("Ожидается опция %1 вместо %2", 
		ВосстановитьТиреПередОпцией(Имя), 
		ВосстановитьТиреПередОпцией(ИмяВместо));
	Возврат НоваяОшибка(Текст, 1);
КонецФункции

#КонецОбласти

#Область ОшибкиАргумента

Функция ОшибкаОжидаетсяАргумент(Имя) Экспорт
	Текст = СтрШаблон("Ожидается аргумент %1", Имя);
	Возврат НоваяОшибка(Текст, 1);
КонецФункции

Функция ОшибкаОжидаетсяАргументВместоОпции(Имя, ИмяОпции) Экспорт
	Текст = СтрШаблон("Ожидается аргумент %1 вместо опции %2", Имя, ВосстановитьТиреПередОпцией(ИмяОпции));
	Возврат НоваяОшибка(Текст, 1);
КонецФункции

#КонецОбласти

Функция ВосстановитьТиреПередОпцией(Имя)
	
	Если СтрНачинаетсяС(Имя, "-") Тогда
		Возврат Имя;
	КонецЕсли;

	Если СтрДлина(Имя) > 1 Тогда
		Возврат "--" + Имя;
	Иначе
		Возврат "-" + Имя;
	КонецЕсли;
	
КонецФункции