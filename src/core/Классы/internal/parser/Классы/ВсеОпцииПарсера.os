#Использовать logos

Перем Опции Экспорт; // Массив - элементы ПараметрКоманды - Ссылка на класс опции
Перем ОпцииИндекс Экспорт; // Соответствие - Ссылка на текущий индекс опций

Перем Лог;

Процедура ПриСозданииОбъекта(ВходящиеОпции, Индекс)
	Опции = ВходящиеОпции;
	Лог.Отладка("Тип входящей опции: %1", ТипЗнч(ВходящиеОпции));
	
	ОпцииИндекс = Индекс;
КонецПроцедуры

// Выполняет поиск опций в массиве входящих аргументов
//
// Параметры:
//   ВходящиеАргументы - Массив - входящие аргументы приложения
//   КонтекстПоиска - Объект - ссылка на класс КонтекстПарсера
//
//  Возвращаемое значение:
//   Объект.РезультатПоискаПараметра - ссылка на класс РезультатПоискаПараметра
//
Функция Поиск(Знач ВходящиеАргументы, КонтекстПоиска) Экспорт

	Аргументы = Новый Массив;

	Для каждого Арг Из ВходящиеАргументы Цикл
		Аргументы.Добавить(Арг);
	КонецЦикла;

	РезультатПопыткиПоиска = ПопыткаПоиска(Аргументы, КонтекстПоиска);

	Если НЕ РезультатПопыткиПоиска.Найден Тогда
		Возврат Новый РезультатПоискаПараметра(Аргументы, Ложь, РезультатПопыткиПоиска.Ошибка);
	КонецЕсли;

	АргументыДляЦикла = РезультатПопыткиПоиска.Аргументы;
	
	Пока Истина Цикл

		РезультатПопыткиПоискаВЦикле = ПопыткаПоиска(АргументыДляЦикла, КонтекстПоиска);
		
		Если НЕ РезультатПопыткиПоискаВЦикле.Найден Тогда
			Возврат Новый РезультатПоискаПараметра(РезультатПопыткиПоискаВЦикле.Аргументы, Истина);
		КонецЕсли;
		
		АргументыДляЦикла = РезультатПопыткиПоискаВЦикле.Аргументы;

	КонецЦикла;
	
	Возврат Новый РезультатПоискаПараметра(Аргументы, Ложь);

КонецФункции

Функция ПопыткаПоиска(Знач Аргументы, КонтекстПоиска)

	Если Аргументы.Количество() = 0 
		ИЛИ КонтекстПоиска.СбросОпций Тогда
		Возврат Новый РезультатПоискаПараметра(Аргументы, Ложь);
	КонецЕсли;

	Для каждого ОпцияПоиска Из Опции Цикл
		
		Если Не КонтекстПоиска.НеВключенныеОпции[ОпцияПоиска.Значение] = Неопределено Тогда
			Лог.Отладка("Исключен поиск опцию %1", ОпцияПоиска.Ключ.Имя);
			Продолжить;
		КонецЕсли;

		Лог.Отладка("Ищу опцию %1", ОпцияПоиска.Ключ.Имя);
		Лог.Отладка("Ищу опцию тип %1", ОпцияПоиска.Значение);
		
		КлассПоиска = Новый ОпцияПарсера(ОпцияПоиска.Значение, ОпцииИндекс);
		РезультатПоиска = КлассПоиска.Поиск(Аргументы, КонтекстПоиска);
		
		Лог.Отладка("Длина аргументов <%1> ", Аргументы.Количество());
		Лог.Отладка("Результат поиска опции %1 = <%2>", ОпцияПоиска.Ключ.Имя, РезультатПоиска.Найден);
		Лог.Отладка("Длина аргументов после поиска <%1> ", РезультатПоиска.Аргументы.Количество());
		
		Если РезультатПоиска.Найден Тогда
			
			Если ОпцияПоиска.Значение.УстановленаИзПеременнойОкружения Тогда
				
				КонтекстПоиска.НеВключенныеОпции.Вставить(ОпцияПоиска.Значение, Истина);
	
			КонецЕсли;
			
			Возврат РезультатПоиска;
		КонецЕсли;

	КонецЦикла;

	Возврат Новый РезультатПоискаПараметра(Аргументы, Ложь);

КонецФункции

// Возвращает приоритет текущего парсера
//
//  Возвращаемое значение:
//   число - приоритет текущего парсера
//
Функция Приоритет() Экспорт
	Возврат 2;
КонецФункции

// Возвращает имя текущего парсера
//
//  Возвращаемое значение:
//   строка - имя текущего парсера, на основании имени опции
//
Функция ВСтроку() Экспорт
	Представление = "-";
	ДлинаОпции = 2;

	Для каждого Опция Из Опции Цикл

		ИмяОпции = Опция.Ключ.Синонимы[0];
		Если СтрНачинаетсяС(ИмяОпции, "-") Тогда
			ИмяОпции = Сред(ИмяОпции, ДлинаОпции);
		КонецЕсли;
			
		Представление = Представление + ИмяОпции;
			
	КонецЦикла;

	Возврат Представление;
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.cli_class_options");