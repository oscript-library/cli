
#Использовать logos
перем Лог;

Перем Завершено Экспорт;
Перем МассивСоединений Экспорт;

Процедура ПриСозданииОбъекта()
	
	МассивСоединений = Новый Массив;
	Завершено = Ложь;

КонецПроцедуры

Функция Т(Парсер, СледующееСостояние) Экспорт
	
	МассивСоединений.Добавить(НовоеСоединениеСовпадений(Парсер, СледующееСостояние));
	Возврат СледующееСостояние;

КонецФункции

Функция НовоеСоединениеСовпадений(Парсер, СледующееСостояние)

	Возврат Новый Структура("Парсер, СледующееСостояние", Парсер, СледующееСостояние);
	
КонецФункции

Процедура Подготовить() Экспорт
	
	ПосетилиСостояниеСортировки = Новый Соответствие;
	ПосетилиСостояниеУпрощения = Новый Соответствие;
	fsm = новый ВыборСовпадений();
	fsm.СортировкаСоединений(ЭтотОбъект, ПосетилиСостояниеСортировки);
	//fsm.УпроститьСоединения(ЭтотОбъект, ЭтотОбъект, ПосетилиСостояниеУпрощения); // тут косяк

КонецПроцедуры

Процедура Прочитать(ВходящиеАргументы) Экспорт
	
	Контекст = Новый КонтекстПарсеров;
	
	ПрименитьКонтекст(ВходящиеАргументы, Контекст);

	fsm = новый ВыборСовпадений();
	
	fsm.ЗаполнитьЗначения(Контекст.Опции);

	fsm.ЗаполнитьЗначения(Контекст.Аргументы);
	
	Лог.Отладка("Проверка контекста: 
	| количество опций: %1", Контекст.Опции.Количество());
	
КонецПроцедуры

Функция ПрименитьКонтекст(Знач ВходящиеАргументы, Контекст) Экспорт
	
	Лог.Отладка("Применяю контекст: 
	| количество соединений %1
	| количество входящих аргументов %2
	| Завершено: %3", МассивСоединений.Количество(), ВходящиеАргументы.Количество(), Завершено);
	
	Если Завершено 
		И ВходящиеАргументы.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;

	Если ВходящиеАргументы.Количество() > 0 Тогда
		
		ПервыйАргумент = ВходящиеАргументы[0];
		Если Не Контекст.СбросОпций
			И СокрЛП(ПервыйАргумент) = "--" Тогда
			Контекст.СбросОпций = Истина;
			ВходящиеАргументы.Удалить(0);
		КонецЕсли;

	КонецЕсли;

	МассивСовпадений = Новый Массив;

	Для каждого Соединение Из МассивСоединений Цикл
		ЧистыйКонтекст = Новый КонтекстПарсеров;
		ЧистыйКонтекст.СбросОпций = Контекст.СбросОпций;

		РезультатПоиска = Соединение.Парсер.Поиск(ВходящиеАргументы, ЧистыйКонтекст);
		
		Лог.Отладка("Нашли опции: %2 %1", РезультатПоиска.РезультатПоиска, Соединение.Парсер );
		лог.Отладка("Количество опций в контексте: %1", ЧистыйКонтекст.Опции.Количество());
		Если РезультатПоиска.РезультатПоиска Тогда
			Лог.Отладка("Добавляю в массив найденное значение");
			МассивСовпадений.Добавить(НовоеСовпадение(Соединение, РезультатПоиска.Аргументы, ЧистыйКонтекст));
		КонецЕсли;

	КонецЦикла;
	
	Для каждого ЭлементСовпадения Из МассивСовпадений Цикл

		Лог.Отладка("Следующие состояние: %1", ЭлементСовпадения.Соединение.СледующееСостояние);
		Лог.Отладка("Для соединения беру следующие состояние: %1", ЭлементСовпадения.Соединение.СледующееСостояние.МассивСоединений.Количество());
		
		Если ЭлементСовпадения.Соединение.СледующееСостояние.ПрименитьКонтекст(ЭлементСовпадения.Результат, ЭлементСовпадения.Контекст)  Тогда
			
			Лог.Отладка("Соединяю контексты");
			Контекст.ПрисоединитьКонтекст(ЭлементСовпадения.Контекст);
			Возврат Истина;
		КонецЕсли;

	КонецЦикла;

	Возврат Ложь

КонецФункции
	
Функция НовоеСовпадение(Соединение, Результат, Контекст)
	Возврат Новый Структура("Соединение, Результат, Контекст", Соединение, Результат, Контекст)
КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.1cli_state");
//Лог.УстановитьУровень(УровниЛога.Отладка);